plugins {
    id 'java'
    id 'idea'
}

group 'com.craftinginterpreters'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/main/java"
        }
    }
}

idea {
    module {
        generatedSourceDirs += file "${buildDir}/generated/main/java"
    }
}

task generateAst(type: JavaExec) {
    description "Generate AST classes"
    main 'com.craftinginterpreters.tool.GenerateAst'
    classpath sourceSets.main.runtimeClasspath
    args = [
            "${buildDir}/generated/main/java/com/craftinginterpreters/lox/ast",
            "Expr",
            "Binary   : Expr left, Token operator, Expr right",
            "Grouping : Expr expression",
            "Literal  : Object value",
            "Unary    : Token operator, Expr right"
    ]
}